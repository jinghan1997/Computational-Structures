.include "C:\Users\Asus\Desktop\50002\50002\stdcell.jsim"
.include "C:\Users\Asus\Desktop\50002\50002\nominal.jsim"
.include "C:\Users\Asus\Desktop\50002\50002\8clocks.jsim"
.include "C:\Users\Asus\Desktop\50002\50002\2dcheckoff_3ns.jsim"

.subckt EQ s[31:0] z
XZ1 S[7:0] S[15:8] S[23:16] S[31:24] r1[7:0] nor4
XZ2 r1[1:0] r1[3:2] r1[5:4] r1[7:6] r2[1:0] nand4
XZ3 r2[0] r2[1] z nor2
.ends

.subckt OV xa xb s v
XIV1 xa nxa inverter
XIV2 xb nxb inverter
XIV3 s ns inverter
XAD1 xa xb ns abns nand3
XAD2 nxa nxb s nanbs nand3
XOR1 abns nanbs v nand2
.ends

.subckt PGtop a b Ci P G S
XND1 a b invG nand2
XIV1 invG G inverter
XNR1 a b P xor2
XXR2 P Ci S xor2
.ends

.subckt PGroot Pij Gij Pj1k Gj1k Ci Cjl Co
// Cj+1 = Gij + PijCi
XIV1 Gij invGij inverter
XAD1 Pij Ci K1 nand2
XAD2 K1 invGij Cjl nand2
.connect Ci Co
.ends

.subckt PG Pij Gij Pj1k Gj1k Ci Pik Gik Cj1 Co
// Pik = Pij * Pj1k
XND1 Pij Pj1k invPik nand2
XIV4 invPik Pik inverter

// Gik = Gj1k + Pj1lGij
XIV1 Gj1k invGj1k inverter
XAD1 Pj1k Gij K1 nand2
XAD2 K1 invGj1k Gik  nand2

// Cj1 = Gij + PijCi
XIV2 Gij invGij inverter
XAD3 Pij Ci K2 nand2
XAD4 K2 invGij Cj1  nand2
.connect Ci Co
.ends

.subckt CLA16 Cin A[15:0] XB[15:0] S[15:0]
XPG1 A[15:0] XB[15:0] C[15:0] P1[15:0] G1[15:0] S[15:0] PGtop
XPG2 P1[14:0:2] G1[14:0:2] P1[15:1:2] G1[15:1:2] CX[14:0:2] P2[14:0:2] G2[14:0:2] C[15:1:2] C[14:0:2] PG
XPG3 P2[12:0:4] G2[12:0:4] P2[14:2:4] G2[14:2:4] CY[12:0:4] P3[12:0:4] G3[12:0:4] CX[14:2:4] CX[12:0:4] PG
XPG4 P3[8:0:8] G3[8:0:8] P3[12:4:8] G3[12:4:8] CZ[8:0:8] P4[8:0:8] G4[8:0:8] CY[12:4:8] CY[8:0:8] PG
XRT1 P4[0] G4[0] P4[8] G4[8] Cin CZ[8] CZ[0] PGroot
.ends


.subckt adder32 ALUFN[0] A[31:0] B[31:0] S[31:0] Z V N
XBF1 ALUFN[0] bALUFN[0] buffer_4
XB1 B[31:0] bALUFN[0]#32 XB[31:0] xor2
.connect  bALUFN[0] C[0]

XPG1 A[31:0] XB[31:0] C[31:0] P1[31:0] G1[31:0] S[31:0] PGtop
XPG2 P1[30:0:2] G1[30:0:2] P1[31:1:2] G1[31:1:2] CX[30:0:2] P2[30:0:2] G2[30:0:2] C[31:1:2] C[30:0:2] PG
XPG3 P2[28:0:4] G2[28:0:4] P2[30:2:4] G2[30:2:4] CY[28:0:4] P3[28:0:4] G3[28:0:4] CX[30:2:4] CX[28:0:4] PG
XPG4 P3[24:0:8] G3[24:0:8] P3[28:4:8] G3[28:4:8] CZ[24:0:8] P4[24:0:8] G4[24:0:8] CY[28:4:8] CY[24:0:8] PG
XPG5 P4[16:0:16] G4[16:0:16] P4[24:8:16] G4[24:8:16] C6[16:0:16] P5[16:0:16] G5[16:0:16] CZ[24:8:16] CZ[16:0:16] PG 
XRT1 P5[0] G5[0] P5[16] G5[16] bALUFN[0] C6[16] C6[0] PGroot

XEQ1 S[31:0] z EQ
XOV1 A[31] XB[31] s[31] v OV
.connect S[31] n
.ends